<?xml version="1.0" encoding="utf-8"?>
<view:BaseCad xmlns:fx="http://ns.adobe.com/mxml/2009" 
			  xmlns:s="library://ns.adobe.com/flex/spark" 
			  xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:view="view.*"
			  xmlns:viewStackEffects="org.efflex.mx.viewStackEffects.*"
			  xmlns:valueObjects="valueObjects.*" xmlns:services="services.*"
			  width="100%" height="100%" xmlns:components="view.renderers.flexsamples.components.*"
			  creationComplete="creationCompleteHandler(event)" >
	<fx:Declarations>
		
		<valueObjects:UserProfileVO id="perfilUsuarioVO"/>
		
		<services:UserProfileService id="perfilUsuarioService"/>
		<s:CallResponder id="findAllPerfilUsuario" result="perfilUsuarioService_resultHandler(event)" />
		<s:CallResponder id="delPerfilUsuario">
			<s:result>
				<![CDATA[
					findAllPerfilUsuario.token = perfilUsuarioService.findAll();
					msgGlowl("Perfil Excluído com Sucesso!!");
				]]>
			</s:result>
		</s:CallResponder>

	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import api.model.Model;
			
			import assets.Assets;
			
			import events.LoginEvent;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.mx_internal;
			import mx.effects.Move;
			import mx.effects.Parallel;
			import mx.effects.Resize;
			import mx.events.CloseEvent;
			import mx.events.EffectEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			import mx.utils.StringUtil;
			
			import valueObjects.PermissionVO;
			
			import view.renderers.MyGrowl;
			import view.renderers.MyProxy;
			
			[Bindable]
			public var visao:int;
			
			[Bindable]
			public var _source:ArrayCollection=new ArrayCollection();
			
			private var perfilCadPopUp:PerfilCadPopUp;
			private var proxy:MyProxy;
			
			protected function initPermissoes():void{
				
				for each (var p:PermissionVO in Model.instance.permissoes){
					
					if(p.recurso.id == visao){
						
						if(p.novo == false){
							btnNovo.visible = false;
							btnNovo.includeInLayout = false;
						}
						
						if(p.editar == false){
							btnEditar.visible = false;
							btnEditar.includeInLayout = false;
						}
						
						if(p.excluir == false){
							
							btnExcluir.visible = false;
							btnExcluir.includeInLayout = false;
						}
					}
				}
			}
			
			// Primeira função executada nessa tela, carrega os dados cadastrados na grid.
			protected function creationCompleteHandler(event:FlexEvent):void
			{	
				initPermissoes();
				findAllPerfilUsuario.token = perfilUsuarioService.findAll();
			}
			
			// Gera o efeito da tela saindo de dentro do botão.
			private function abrirJanela(width:int, height:int, x:int, y:int ):void{
				
				var efeito:Parallel = new Parallel();
				var mover:Move = new Move();
				var redim:Resize = new Resize();
				proxy = new MyProxy();
				
				//this.addChild(proxy);
				this.addElement(proxy);
				proxy.width = width;
				proxy.height = height;
				proxy.x = x;
				proxy.y = y;
				
				redim.widthTo = 600;
				redim.heightTo = 500;
				
				mover.xTo = (this.width/2) - 200;
				mover.yTo = (this.height/2) - 150;
				
				efeito.addChild(redim);
				efeito.addChild(mover);
				efeito.duration = 600;
				efeito.addEventListener(EffectEvent.EFFECT_END, janela);
				efeito.play([proxy]);
				
			}
			
			// Abre a janela para salvar um novo registro ou atualizar um ja existente.
			private function janela(e:EffectEvent):void{
				perfilCadPopUp = PopUpManager.createPopUp(
					this, view.usuario.PerfilCadPopUp, true) as PerfilCadPopUp;
				
				// Passa o VO Criado para a outra tela poder manipular.
				perfilCadPopUp.perfilUsuarioVO = perfilUsuarioVO;
				PopUpManager.centerPopUp(perfilCadPopUp);
				
				perfilCadPopUp.addEventListener(LoginEvent.ON_POPUP_REMOVE, onClienteSave);
				this.removeElement(proxy);
			}
			
			// Passa para a função msgGrowl a mensagem de FedBack ao Usuário e atualiza a grid.
			private function onClienteSave(evt:LoginEvent):void
			{
				msgGlowl("Operação realizada com Sucesso!!");
				findAllPerfilUsuario.token = perfilUsuarioService.findAll();
			}
			
			// Filtra a grid pela descrição.
			private function cmdProcurar():void
			{
				txtSearch.text=StringUtil.trim(txtSearch.text);
				
				if (txtSearch.text.length > 0)
					
					_source.filterFunction=filterFunction;
				else
					_source.filterFunction=null;
				_source.refresh();
			}
			
			// Filtra a grid pela descrição
			private function filterFunction(item:UserProfileVO):Boolean
			{
				if (item.descricao == null)
					item.descricao='';
				
				return (item.descricao.toUpperCase().indexOf(txtSearch.text.toUpperCase()) > -1)
			}
			
			// Seta na arraycollection que esta associada a grid o resultado do findAll
			protected function perfilUsuarioService_resultHandler(e:ResultEvent):void{
				
				if (e.result != null)
				{
					_source = e.result as ArrayCollection;
				}
			}
			
			// Cria o PopUp para mostrar o FedBack da ação do Usuário.
			protected function msgGlowl(msg:String):void{
				
				var growl:MyGrowl = PopUpManager.createPopUp(
				this, view.renderers.MyGrowl, false) as MyGrowl;
				growl.msg = msg;
				growl.x = 940;
				growl.y = 110;
			}
			
			protected function excluirPerfilUsuario():void{
				
				var resultAlert:Alert = Alert.show('Deseja mesmo excluir o perfil selecionado?','Pergunta', Alert.OK | Alert.CANCEL, this,validaBtn,null,Alert.OK);
			}
			
			protected function validaBtn(eventObj:CloseEvent):void
			{
				if (eventObj.detail == Alert.OK)
				{
					perfilUsuarioVO = dgPerfilUsuario.selectedItem as UserProfileVO;
					perfilUsuarioVO.excluido = true;
					perfilUsuarioVO.excluido_data = new Date();
					perfilUsuarioVO.excluido_usuario = Model.instance.usuario.loggedUser.usuario;
					delPerfilUsuario.token = perfilUsuarioService.update(dgPerfilUsuario.selectedItem);
				}else{
					msgGlowl("Operação negada pelo usuário!!");
				}
			}
			
			protected function btnNovoCliked():void{
				
				perfilUsuarioVO = new UserProfileVO();
				abrirJanela(btnNovo.width, btnNovo.height, btnNovo.x, btnNovo.y);
			}
			
			protected function btnEditarCliked():void{
				
				perfilUsuarioVO = dgPerfilUsuario.selectedItem as UserProfileVO;
				abrirJanela(btnEditar.width, btnEditar.height, btnEditar.x, btnEditar.y);
			}
			
		]]>
	</fx:Script>
	<mx:VBox width="100%" height="100%">
		<s:HGroup width="100%" paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10">
			
			<!-- BOTÃO NOVO -->
			<components:IconButton label="Novo" id="btnNovo" click="btnNovoCliked()" 
								   icon="@Embed('/assets/images/icontexto-webdev-add.png')"/>
			
			<mx:Spacer width="10"/>
			
			<!-- BOTÃO DE EDITAR -->
			<components:IconButton label="Editar" id="btnEditar" enabled="{dgPerfilUsuario.selectedItem!=null}"
								   icon="@Embed('/assets/images/icontexto-webdev-edit.png')" click="btnEditarCliked()"/>
							
			<mx:Spacer width="10"/>
			
			<!-- BOTÃO DE EXCLUIR -->
			<components:IconButton label="Excluir" id="btnExcluir" enabled="{dgPerfilUsuario.selectedItem!=null}" 
					  			   click="excluirPerfilUsuario()" icon="@Embed('/assets/images/icontexto-webdev-remove.png')"/>
			
		</s:HGroup>
	
		<mx:Form width="100%" right="100%">
			<mx:FormItem label="Descrição:">
				<mx:HBox>
					<mx:TextInput id="txtSearch" width="100%"
								  change="cmdProcurar()"/>
				</mx:HBox>
			</mx:FormItem>
		</mx:Form>
		
		<mx:DataGrid id="dgPerfilUsuario" 
					 y="30" width="100%" left="10" height="100%"
					 dataProvider="{_source}"
					 paddingRight="10" paddingLeft="10" rowHeight="30" borderAlpha="0.4"
					 headerHeight="30" verticalGridLines="false" headerStyleName="centered" headerSeparatorSkin="{Assets.HEADER_SEPARATOR}"
					 >
			<mx:columns>
				<mx:DataGridColumn headerText="Código" dataField="id" width="100"/>
				<mx:DataGridColumn headerText="Descrição" dataField="descricao"/>
			</mx:columns>
		</mx:DataGrid>
	</mx:VBox>
</view:BaseCad>
