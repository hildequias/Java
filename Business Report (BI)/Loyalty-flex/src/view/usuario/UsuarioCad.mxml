<?xml version="1.0" encoding="utf-8"?>
<view:BaseCad xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:view="view.*"
		 xmlns:valueObjects="valueObjects.*" xmlns:services="services.*" 
		 xmlns:components="view.renderers.flexsamples.components.*"
		 width="100%" height="100%"
		 creationComplete="creationCompleteHandler(event)">
	<fx:Declarations>
		
		<mx:DateFormatter id="dateClientFormat" formatString="DD/MM/YYYY"/>
		
		<!-- Objeto VO para ser manipulado -->
		<valueObjects:UserVO id="usuarioVO"/>
		
		<!-- Serviço de comunicação com o Java -->
		<services:UserService id="usuarioService" />
		<s:CallResponder id="findAllUsuario" result="usuarioService_resultHandler(event)" />
		<s:CallResponder id="delUsuario">
			<s:result>
				<![CDATA[
					findAllUsuario.token = usuarioService.findAll();
				]]>
			</s:result>
		</s:CallResponder>
		
		<!-- Serviço de comunicação com o Java referente ao Perfil do Usuario -->
		<services:UserProfileService id="perfilUsuarioService"/>
		<s:CallResponder id="findAllPerfilUsuario" />
		
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import api.model.Model;
			
			import assets.Assets;
			
			import events.LoginEvent;
			
			import flash.net.registerClassAlias;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.effects.Move;
			import mx.effects.Parallel;
			import mx.effects.Resize;
			import mx.events.CloseEvent;
			import mx.events.EffectEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			import mx.utils.StringUtil;
			
			import valueObjects.PermissionVO;
			
			import view.renderers.MyGrowl;
			import view.renderers.MyProxy;
			
			private var proxy:MyProxy;
			private var usuarioCadPopUp:UsuarioCadPopUp;
			
			[Bindable]
			public var visao:int;
			
			[Bindable]
			public var _source:ArrayCollection=new ArrayCollection;
			
			protected function initPermissoes():void{
				
				for each (var p:PermissionVO in Model.instance.permissoes){
					
					if(p.recurso.id == visao){
						
						if(p.novo == false){
							btnNovo.visible = false;
							btnNovo.includeInLayout = false;
						}
						
						if(p.editar == false){
							btnEditar.visible = false;
							btnEditar.includeInLayout = false;
						}
						
						if(p.excluir == false){
							
							btnExcluir.visible = false;
							btnExcluir.includeInLayout = false;
						}
					}
				}
			}
			
			// Primeira função executada nessa tela, carrega os dados cadastrados na grid.
			protected function creationCompleteHandler(event:FlexEvent):void
			{	
				initPermissoes();
				findAllPerfilUsuario.token = perfilUsuarioService.findAll();
				findAllUsuario.token = usuarioService.findAll();
			}
			
			// Seta na arraycollection que esta associada a grid o resultado do findAllUsuario
			protected function usuarioService_resultHandler(e:ResultEvent):void{
				
				if (e.result != null)
				{
					_source = e.result as ArrayCollection;
				}
			}
			
			// Filtra a grid pelo usuario.
			protected function cmdProcurar():void
			{
				txtSearch.text=StringUtil.trim(txtSearch.text);
				
				if (txtSearch.text.length > 0)
					
					_source.filterFunction=filterFunction;
				else
					_source.filterFunction=null;
				_source.refresh();
			}
			
			// Filtra a grid pelo usuario
			protected function filterFunction(item:UserVO):Boolean
			{
				if (item.usuario == null)
					item.usuario='';
				
				return (item.usuario.toUpperCase().indexOf(txtSearch.text.toUpperCase()) > -1)
			}
			
			protected function getPerfilUsuarioLabel(data:Object, column:DataGridColumn):String
			{
				if (data.perfilUsuario!=null)
					return data.perfilUsuario.descricao;
				
				return "Indefinido";
			}
			
			protected function getDateLabel(data:Object, column:DataGridColumn):String
			{
				return dateClientFormat.format(data.data_ultimo_login);
			}
			
			protected function btnNovoCliked():void{
				
				usuarioVO = new UserVO();
				abrirJanela(btnNovo.width, btnNovo.height, btnNovo.x, btnNovo.y);
			}
			
			// Gera o efeito da tela saindo de dentro do botão.
			private function abrirJanela(width:int, height:int, x:int, y:int ):void{
				
				var efeito:Parallel = new Parallel();
				var mover:Move = new Move();
				var redim:Resize = new Resize();
				proxy = new MyProxy();
				
				//this.addChild(proxy);
				this.addElement(proxy);
				proxy.width = width;
				proxy.height = height;
				proxy.x = x;
				proxy.y = y;
				
				redim.widthTo = 600;
				redim.heightTo = 500;
				
				mover.xTo = (this.width/2) - 200;
				mover.yTo = (this.height/2) - 150;
				
				efeito.addChild(redim);
				efeito.addChild(mover);
				efeito.duration = 600;
				efeito.addEventListener(EffectEvent.EFFECT_END, janela);
				efeito.play([proxy]);
				
			}
			
			// Abre a janela para salvar um novo registro ou atualizar um ja existente.
			private function janela(e:EffectEvent):void{
				usuarioCadPopUp = PopUpManager.createPopUp(
					this, view.usuario.UsuarioCadPopUp, true) as UsuarioCadPopUp;
				
				// Passa o VO Criado para a outra tela poder manipular.
				usuarioCadPopUp.usuarioVO = usuarioVO;
				PopUpManager.centerPopUp(usuarioCadPopUp);
				
				usuarioCadPopUp.addEventListener(LoginEvent.ON_POPUP_REMOVE, onUsuarioSave);
				this.removeElement(proxy);
			}
			
			// Passa para a função msgGrowl a mensagem de FedBack ao Usuário e atualiza a grid.
			private function onUsuarioSave(evt:LoginEvent):void
			{
				msgGlowl("Registro Salvo com Sucesso!!");
				findAllUsuario.token = usuarioService.findAll();
			}
			
			// Cria o PopUp para mostrar o FedBack da ação do Usuário.
			protected function msgGlowl(msg:String):void{
				
				var growl:MyGrowl = PopUpManager.createPopUp(
					this, view.renderers.MyGrowl, false) as MyGrowl;
				growl.msg = msg;
				growl.x = 940;
				growl.y = 110;
			}
			
			protected function btnEditarCliked():void{
				
				usuarioVO = dgUsuario.selectedItem as UserVO;
				abrirJanela(btnEditar.width, btnEditar.height, btnEditar.x, btnEditar.y);
			}
			
			protected function excluirUsuario():void{
				
				var resultAlert:Alert = Alert.show('Deseja mesmo excluir o Usuário selecionado?','Pergunta', Alert.OK | Alert.CANCEL, this,validaBtn,null,Alert.OK);
			}
			
			protected function validaBtn(eventObj:CloseEvent):void
			{
				if (eventObj.detail == Alert.OK)
				{
					usuarioVO = dgUsuario.selectedItem as UserVO;
					usuarioVO.excluido = true;
					usuarioVO.excluido_data = new Date();
					usuarioVO.excluido_usuario = Model.instance.usuario.loggedUser.usuario;
					delUsuario.token = usuarioService.update(dgUsuario.selectedItem);
				}else{
					msgGlowl("Operação negada pelo usuário!!");
				}
			}
			
		]]>
	</fx:Script>
	
	<mx:VBox width="100%" height="100%" >
		<s:HGroup width="100%" paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10">
			
			<!-- BOTÃO NOVO -->
			<components:IconButton label="Novo" id="btnNovo" click="btnNovoCliked()"
								   icon="@Embed('/assets/images/icontexto-webdev-add.png')"/>
			
			<mx:Spacer width="10"/>
			
			<!-- BOTÃO DE EDITAR -->
			<components:IconButton label="Editar" id="btnEditar" enabled="{dgUsuario.selectedItem!=null}"
								   icon="@Embed('/assets/images/icontexto-webdev-edit.png')" click="btnEditarCliked()" />
			
			<mx:Spacer width="10"/>
			
			<!-- BOTÃO DE EXCLUIR -->
			<components:IconButton label="Excluir" enabled="{dgUsuario.selectedItem!=null}" click="excluirUsuario()"
								   id="btnExcluir" icon="@Embed('/assets/images/icontexto-webdev-remove.png')"/>
		</s:HGroup>
		
		<mx:Form width="100%" right="100%">
			<mx:FormItem label="Login:">
				<mx:HBox>
					<mx:TextInput id="txtSearch" width="100%"
								  change="cmdProcurar()"/>
				</mx:HBox>
			</mx:FormItem>
		</mx:Form>
		
		<mx:DataGrid id="dgUsuario" 
					 y="30" width="100%" left="10" height="100%"
					 dataProvider="{_source}"
					 paddingRight="10" paddingLeft="10" rowHeight="30" borderAlpha="0.4"
					 headerHeight="30" verticalGridLines="false" headerStyleName="centered" headerSeparatorSkin="{Assets.HEADER_SEPARATOR}"
					 >
			<mx:columns>
				<mx:DataGridColumn headerText="Código" dataField="id" width="100"/>
				<mx:DataGridColumn headerText="Usuário" dataField="usuario"/>
				<mx:DataGridColumn headerText="Email" dataField="email"/>
				<mx:DataGridColumn headerText="Nome" dataField="nome_completo"/>
				<mx:DataGridColumn headerText="Ultimo_login" dataField="data_ultimo_login"
								   labelFunction="getDateLabel"/>
				<mx:DataGridColumn headerText="Perfil" dataField="perfilUsuario"
								   labelFunction="getPerfilUsuarioLabel" />
			</mx:columns>
		</mx:DataGrid>
	</mx:VBox>
</view:BaseCad>
